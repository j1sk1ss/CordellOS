from SCons.Environment import Environment

from build_scripts.utility import glob_recursive

Import('TARGET_ENVIRONMENT')
TARGET_ENVIRONMENT: Environment

env = TARGET_ENVIRONMENT.Clone()
env.Append(
    ASFLAGS = [
        f'-DFILESYSTEM={TARGET_ENVIRONMENT["image_file_system"]}',
        f'-I', env.Dir('.').srcnode()
    ],
    LINKFLAGS = [
        '-Wl,-T', env.File('linker.ld').srcnode().path,
        '-Wl,-Map=' + env.File('fst_stg.map').path
    ],
)

inputs = glob_recursive(env, "*.asm")
headers = glob_recursive(env, "*.inc")

objects = env.Object(inputs)

fst_stg = env.Program('fst_stg.bin', objects)
env.Depends(fst_stg, headers)

Export('fst_stg')