import os

from SCons.Environment import Environment
from build_scripts.utility import glob_recursive


Import('TARGET_ENVIRONMENT')
TARGET_ENVIRONMENT: Environment

env = TARGET_ENVIRONMENT.Clone()
env.Append(
    CCFLAGS=[
        '-c', '-fpic'
    ],

    LINKFLAGS = [
        '-Wl,-Map=' + env.File('userl.map').path
    ],

    CPPPATH = [ 
        env.Dir('.').srcnode(),
        env['PROJECTDIR'].Dir('src/libs')
    ],

    ASFLAGS = [ '-I', env.Dir('.').srcnode(), '-f', 'elf' ]
)

# Add boot.s to the sources
sources = glob_recursive(env, '*.c') + \
          glob_recursive(env, '*.cpp') + \
          glob_recursive(env, '*.asm')

headers = glob_recursive(env, '*.h') + \
          glob_recursive(env, '*.hpp') + \
          glob_recursive(env, '*.inc')

objects = env.Object(sources)

Import('libcore')

static_libs = [
    libcore
]

objects = [
    *objects,
    *static_libs,
]

userl = env.Program('userl.elf', objects)
userl_stripped = env.Command('userl-s.elf', userl, '$STRIP -o $TARGET $SOURCE')

env.Default(userl_stripped)

Export('userl')
Export('userl_stripped')