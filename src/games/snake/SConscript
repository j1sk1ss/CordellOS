import os

from SCons.Environment import Environment
from build_scripts.utility import glob_recursive, find_index, is_file_name


Import('TARGET_ENVIRONMENT')
TARGET_ENVIRONMENT: Environment

env = TARGET_ENVIRONMENT.Clone()
env.Append(
    LINKFLAGS = [
        '-Wl,-T', env.File('linker.ld').srcnode().path,
        '-Wl,-Map=' + env.File('snake.map').path,
        '-nostdlib',
    ],

    CPPPATH = [ 
        env.Dir('.').srcnode(),
        env['PROJECTDIR'].Dir('src/libs')
    ],

    ASFLAGS = [ '-I', env.Dir('.').srcnode(), '-f', 'elf' ]
)

# Add boot.s to the sources
sources = glob_recursive(env, '*.c') + \
          glob_recursive(env, '*.cpp') + \
          glob_recursive(env, '*.asm')

headers = glob_recursive(env, '*.h') + \
          glob_recursive(env, '*.hpp') + \
          glob_recursive(env, '*.inc')

objects = env.Object(sources)

Import('libcore')

static_libs = [
    libcore
]

objects = [
    *objects,
    *static_libs,
]

snake = env.Program('snake.elf', objects)
snake_stripped = env.Command('snake-stripped.elf', snake, '$STRIP -o $TARGET $SOURCE')

env.Default(snake_stripped)

Export('snake')
Export('snake_stripped')